# Find the number of South American soccer players who (appear in Wikipedia and) 
# have played for a club in the Spanish “La Liga” that has never been relegated, 
# grouping by country.


PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dbc: <http://dbpedia.org/resource/Category:>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbp: <http://dbpedia.org/property/>
SELECT ?country (COUNT(?player) as ?num_players)  WHERE {
  ?player rdf:type dbo:SoccerPlayer ;
          dct:subject dbc:La_Liga_players, dbc:Expatriate_footballers_in_Spain ;
          dbp:clubs ?club ;
          dbo:birthPlace ?city .
  ?club rdf:type dbo:SoccerClub ;
        dct:subject dbc:La_Liga_clubs, dbc:Unrelegated_association_football_clubs .
  ?city rdf:type dbo:City ;
        dbo:country ?country .
  ?country rdf:type dbo:Country ;
           dct:subject dbc:Countries_in_South_America .
           
}
GROUP BY ?country
ORDER BY DESC(?num_players)

# RESULT: 13

